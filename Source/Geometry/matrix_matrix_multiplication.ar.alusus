اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛

استخدم مـتم؛

عرف مـصفوفة_ثنائية: لقب مـصفوفة[مـصفوفة[عـائم[64]]]؛


دالة جداء(م1: سند[مـصفوفة_ثنائية]، م2: سند[مـصفوفة_ثنائية]): مـصفوفة_ثنائية {
	عرف أسطر_م1: صـحيح = م1.هات_الطول()؛
	عرف أسطر_م2: صـحيح = م2.هات_الطول()؛
	عرف أعمدة_م1: صـحيح = م1(0).هات_الطول()؛
	عرف أعمدة_م2: صـحيح = م2(0).هات_الطول()؛

	عرف نتيجة: مـصفوفة_ثنائية؛
	
	عرف دليل1: صـحيح؛
	عرف دليل2: صـحيح؛
	عرف دليل3: صـحيح؛
	لكل دليل1 = 0 ، دليل1 < أسطر_م1 ، دليل1++ {
		عرف سطر: مـصفوفة[عـائم[64]]؛
		لكل دليل2 = 0 ، دليل2 < أعمدة_م2 ، دليل2++ {
			سطر.أضف(0)؛
			لكل دليل3 = 0 ، دليل3 < أعمدة_م1 ، دليل3++ {
				سطر(دليل2) += م1(دليل1)(دليل3) * م2(دليل3)(دليل2)؛
			}
		}
		نتيجة.أضف(سطر)؛
	}

	ارجع نتيجة؛
}

دالة رئيسي() {
	عرف م1: مـصفوفة_ثنائية؛
	م1.أضف(مـصفوفة[عـائم[64]]({1.0, 2.0, 3.0}))؛
	م1.أضف(مـصفوفة[عـائم[64]]({4.0, 5.0, 6.0}))؛
	عرف م2: مـصفوفة_ثنائية؛
	م2.أضف(مـصفوفة[عـائم[64]]({7.0, 8.0}))؛
	م2.أضف(مـصفوفة[عـائم[64]]({9.0, 10.0}))؛
	م2.أضف(مـصفوفة[عـائم[64]]({11.0, 12.0}))؛
	
		
	عرف نتيجة: مـصفوفة_ثنائية = جداء(م1، م2)؛
	
	عرف دليل1: صـحيح؛
	عرف دليل2: صـحيح؛
	لكل دليل1 = 0 ، دليل1 < نتيجة.هات_الطول() ، دليل1++ {
		لكل دليل2 = 0 ، دليل2 < نتيجة(0).هات_الطول() ، دليل2++ {
			طـرفية.اطبع("%.2f "، نتيجة(دليل1)(دليل2))؛
		}
		طـرفية.اطبع("\ج")؛
	}
}

رئيسي()؛
