اشمل "مـتم/مـصفوفة"؛

استخدم مـتم؛

دالة دفس(عقدة: صـحيح، مزارة: سند[مـصفوفة[ثـنائي]]، بيان: سند[مـصفوفة[مـصفوفة[صـحيح]]]) {
	مزارة(عقدة) = صح؛
	
	عرف دليل: صـحيح؛
	لكل دليل = 0 ، دليل < بيان(عقدة).هات_الطول() ، دليل++ {
		عرف عقدة_ابن: صـحيح = بيان(عقدة)(دليل)؛
		إذا !مزارة(عقدة_ابن) {
			دفس(عقدة_ابن، مزارة، بيان)؛
		}
	}
}


دالة عدد_المكونات_المترابطة(بيان: سند[مـصفوفة[مـصفوفة[صـحيح]]]): صـحيح {
	عرف مزارة: مـصفوفة[ثـنائي]؛
	عرف دليل: صـحيح؛
	لكل دليل = 0 ، دليل < بيان.هات_الطول() ، دليل++ {
		مزارة.أضف(خطأ)؛
	}
	
	عرف عدد_المكونات: صـحيح = 0؛
	لكل دليل = 0 ، دليل < بيان.هات_الطول() ، دليل++ {
		إذا !مزارة(دليل) {
			دفس(دليل، مزارة، بيان)؛
			عدد_المكونات++؛
		}
	}
	
	ارجع عدد_المكونات؛
}
