import "Srl/Console";
import "Srl/Array";

use Srl;

func dfs(node: Int, parentNode: Int, visited: ref[Array[Bool]], graph: ref[Array[Array[Int]]]): Bool {
    visited(node) = true;

    def i: Int;
    for i = 0 , i < graph(node).getLength(), i++ {
        def childNode: Int = graph(node)(i);
        if !visited(childNode) {
            if dfs(childNode, node, visited, graph) {
                return true;
            }
        }
        else if childNode != parentNode {
            return true;
        }
    }
    return false;
}


func isTree(graph: ref[Array[Array[Int]]]): Bool {
    def visited: Array[Bool];
    def i: Int;
    for i = 0 , i < graph.getLength() , i++ {
        visited.add(false);
    }

    if dfs(0, -1, visited, graph) {
        return false;
    }

    for i = 0 , i < graph.getLength() , i++ {
        if !visited(i) {
            return false;
        }
    }

    return true;
}

func main() {
    def graph: Array[Array[Int]];
    graph.add(Array[Int]({1}));
    graph.add(Array[Int]({0, 2, 3}));
    graph.add(Array[Int]({1}));
    graph.add(Array[Int]({1, 4, 5}));
    graph.add(Array[Int]({3}));
    graph.add(Array[Int]({3}));

    if isTree(graph) {
        Console.print("The graph is a tree\n");
    }
    else {
        Console.print("The graph is not a tree\n");
    }
}

main();
