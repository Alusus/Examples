اشمل "مـتم/مـصفوفة"؛

استخدم مـتم؛


دالة ادمج(م: سند[مـصفوفة[صـحيح]]، يسار: صـحيح، وسط: صـحيح، يمين: صـحيح) {
	عرف دليل1: صـحيح؛
	عرف دليل2: صـحيح؛
	عرف دليل3: صـحيح؛
	
	عرف حجم1: صـحيح = وسط - يسار + 1؛
	عرف حجم2: صـحيح = يمين - وسط؛
	
	عرف م_يسار: مـصفوفة[صـحيح]؛
	عرف م_يمين: مـصفوفة[صـحيح]؛
	
	لكل دليل1 = 0 ، دليل1 < حجم1 ، دليل1++ {
		م_يسار.أضف(م(يسار+دليل1))؛
	}
	لكل دليل1 = 0 ، دليل1 < حجم2 ، دليل1++ {
		م_يمين.أضف(م(وسط + 1 + دليل1))؛
	}
	
	دليل1 = 0؛
	دليل2 = 0؛
	دليل3 = يسار؛
	
	بينما دليل1 < حجم1 && دليل2 < حجم2 {
		إذا م_يسار(دليل1) <= م_يمين(دليل2) {
			م(دليل3) = م_يسار(دليل1)؛
			دليل1++؛
		}
		وإلا {
			م(دليل3) = م_يمين(دليل2)؛
			دليل2++؛
		}
		دليل3++؛
	}
	
	بينما دليل1 < حجم1 {
		م(دليل3) = م_يسار(دليل1)؛
		دليل1++؛
		دليل3++؛
	}
	
	بينما دليل2 < حجم2 {
		م(دليل3) = م_يمين(دليل2)؛
		دليل2++؛
		دليل3++؛
	}
}


دالة ترتيب_بالدمج(م: سند[مـصفوفة[صـحيح]]، يسار: صـحيح، يمين: صـحيح) {
	إذا يسار < يمين {
		عرف وسط: صـحيح = يسار + (يمين - يسار) / 2؛
		
		ترتيب_بالدمج(م، يسار، وسط)؛
		ترتيب_بالدمج(م، وسط + 1، يمين)؛
		
		ادمج(م، يسار، وسط، يمين)؛
	}
}
