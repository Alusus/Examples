```
import "Build";
import "Apm";
Apm.importFile("Alusus/WebPlatform");
use Srl;
use WebPlatform;


def lastId: Int = 0;

class Post {
    def id: Int;
    def title: String;
    def content: String;
    def imgData: String;

    handler this~init() {
        this.id = lastId++;
        this.title = "";
        this.content = "";
        this.imgData = "";
    }

    handler this~init(title: ref[String], content: ref[String], imgData: ref[String]) {
        this.id = lastId++;
        this.title = title;
        this.content = content;
        this.imgData = imgData;
    }

    handler this~init(post: ref[Post]) {
        this.id = lastId++;
        this.title = post.title;
        this.content = post.content;
        this.imgData = post.imgData;
    }

    handler this = ref[Post] {
        this.id = lastId++;
        this.title = value.title;
        this.content = value.content;
        this.imgData = value.imgData;
    }
}


def posts: Map[Int, Post];


@beEndpoint["POST", "/post"]
func addPost (conn: ptr[Http.Connection]) {
    def postData: array[Char, 1048576];
    // retrieve the sent information and store it in the buffer
    def postDataSize: Int = Http.read(conn, postData~ptr, postData~size);
    
    def data: Json = postData~ptr;

    def imgData: String = data("imgData");

    def title: String =  data("title");
    def content: String = data("content");

    posts.set(lastId, Post(title, content, imgData));

    Http.print(conn, "HTTP/1.1 201 Created\r\n\r\n");
}
```
